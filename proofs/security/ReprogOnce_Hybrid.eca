require import AllCore Distr DBool PROM List StdOrder FelTactic.
(*---*) import StdBigop.Bigreal.BRA.

require import Dexcepted.
require import Dfilter.
require import Real RealSeries.
require import DistrExtras.

require GuessReprog.
import Biased.
import SmtMap.

(* Define some necessary abstract stuff *)

type M, W, C, Z, ST, PK, SK.

op [lossless] keygen : (PK * SK) distr.
op [lossless] commit : SK -> (W * ST) distr.
op [lossless uniform] dC : C distr.
op respond : SK -> ST -> C -> Z option.

op valid_sk sk =
  exists pk, (pk, sk) \in keygen.

(* FIXME : this need to be properly defined *)
op p : real.     (* max proba of rejection *)
op eps : real.   (* max proba of = w *)

axiom p_def sk : 
  valid_sk sk => 
  mu1 (dlet (commit sk) (fun (w_st : W * ST) =>              
    dmap dC (fun (c : C) =>
      respond sk w_st.`2 c))) None <= p.

axiom p_bounded : 0%r <= p < 1%r.

axiom valid_sk_mu1_commit sk : 
  valid_sk sk => 
  forall x, mu1 (dfst (commit sk)) x <= eps.

(* This should be provable from valid_sk_mu1_commit *)
axiom eps_ge0 : 0%r <= eps. 

 (* max. number of signing queries *)
op qs : {int | 0 <= qs} as qs_ge0.
(* max. number of hash queries *)
op qh : {int | 0 <= qh} as qh_ge0.
(* max. number of iterations *)
op kappa : {int | 0 < kappa} as kappa_gt0.

clone import PROM.FullRO as RO with
  type in_t <= W * M,
  type out_t <= C,
  op dout <= (fun _ => dC)
proof *.

module type Oracle = {
  proc h(w: W, m: M) : C
  proc sign(m: M) : (W * C * Z) option
}.

module type Oracle_i = {
  include Oracle
  proc init(sk_i : SK) : unit
}.


module Count (O: Oracle_i) : Oracle = {
  var countS : int
  var countH : int

  proc init = O.init 

  proc h(w: W, m : M) : C = {
    var y;
    countH <- countH + 1;
    y <@ O.h((w, m));
    return y;
  }

  proc sign(x) = {
    var y;
    countS <- countS + 1;
    y <@ O.sign(x);
    return y;
  }
}.

module CountBound (O: Oracle_i) : Oracle = {
  import var Count

  proc init (sk : SK) = {
    O.init(sk);
    countS <- 0;
    countH <- 0;
  }
  
  proc h(w : W, m : M) : C = {
    var y;
    y <- witness;
    if (countH < qh) { 
      y <@ O.h((w, m));
      countH <- countH + 1;
    }
    return y;
  }

  proc sign(x) = {
    var y;
    y <- witness;
    if (countS < qs) { 
      y <@ O.sign(x);
      countS <- countS + 1;
    }
    return y;
  }
}.

module OLeft : Oracle_i = {
  var sk : SK

  proc init(sk_i: SK) = {
    sk <- sk_i;
  }

  proc h = RO.get

  proc sign(m: M) = {
    var w, st, c, oz, i;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    i <- 0;
    oz <- None;
    while(oz = None /\ i < kappa) {
      (w, st) <$ commit sk;
      c <$ dC;
      oz <- respond sk st c;
      RO.set((w, m), c);
      i <- i + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }
}.

(* Moving RO.set outside while-loop *)
module ORight : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, st, c, oz, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    i <- 0;
    oz <- None;
    while(oz = None /\ i < kappa) {
      (w, st) <$ commit sk;
      c <$ dC;
      oz <- respond sk st c;
      i <- i + 1;
    }
    if(oz <> None)
      RO.set((w, m), c);
    return if oz <> None then Some (w, c, oget oz) else None;
  }
}.

module type Adversary(O: Oracle) = {
  proc distinguish(pk: PK): bool
}.

module Game (Adv: Adversary) (O: Oracle_i) = {
  proc main() = {
    var b, pk, sk;
    (pk, sk) <$ keygen;
    RO.init();
    O.init(sk);
    b <@ Adv(O).distinguish(pk);
    return b;
  }
}.

module GameI (Adv: Adversary) (O: Oracle_i) = {
  proc main() = {
    var b, pk, sk;
    (pk, sk) <$ keygen;
    RO.init();
    O.init(sk);
    Count.countS <- 0;
    Count.countH <- 0;
    b <@ Adv(O).distinguish(pk);
    return b;
  }
}.


(* This lemma should be moved to EC *)
lemma hybrid_sum (f: int -> real) (m n:int) : m <= n =>
  f(n) - f(m) = bigi predT (fun i => f(i+1) - f(i)) m n.
proof.
  move=> /IntOrder.ler_eqVlt [<<- | hmn].
  + by rewrite big_geq 1://; ring.
  rewrite -sumrB (big_ltn m n f) 1:// /=.
  have heq: n = n - 1 + 1 by ring.
  rewrite heq (big_int_recr (n-1) m) 1:/# -heq /=; ring.
  rewrite (big_reindex _ _ (fun x=> x - 1) (fun x=> x + 1) (range m (n - 1))) //.
  have ->: (transpose Int.(+) 1) = ((+) 1).
  + by apply: fun_ext=> x; ring.
  have ->: predT \o transpose Int.(+) (-1) = predT by done.
  by rewrite /(\o) /= -(range_addl m n 1).
qed. 

(* Move this to EC *)
lemma sum_pow (p:real) (n : int) : 0 <= n => p <> 1%r =>
  bigi predT (fun i => p^i) 0 n = (1%r - p^n) / (1%r - p).
proof.
  move=> hn hp; have := StdBigop.Bigreal.subrXX 1%r p n hn.
  rewrite RField.expr1z -(eq_big_int _ _ (fun (i : int) => p ^ i)).
  + by move=> i hi /=; rewrite RField.expr1z.
  move=> ->; field => /#.
qed.

(* Move this to EC ? *)
lemma sum_pow_le (p:real) (n : int) : 0 <= n => 0%r <= p < 1%r =>
  bigi predT (fun i => p^i) 0 n <= 1%r / (1%r - p).
proof.
  move=> hn hp; rewrite sum_pow // 1:/#.
  smt(RealOrder.expr_ge0  RealOrder.exprn_ilt1 sum_pow).
qed.

abstract theory Analysis.

module type Oracle_i_i = {
  include Oracle
  proc init(sk_i : SK, i:int) : unit
 }.

module type Oracle_i_ij = {
  include Oracle
  proc init(sk_i : SK, i:int, j:int) : unit
 }.

module Oi : Oracle_i_i = {
  var sk : SK
  var i_ : int
  var j_ : int
  var j_end : bool
  var wo : W option
  var w_ : W
  var logh : W list

  var countS : int
  var countH : int

  proc init(sk_i:SK, i_i:int) = { 
    i_ <- i_i;
    sk <- sk_i;
    countS <- 0;
    countH <- 0;
    logh <- [];
  }

  proc h(w : W, m : M) : C = { 
    var y;
    y <- witness;
    if (countH < qh) {
      y <@ RO.get((w, m));
      countH <- countH + 1;
      logh <- w::logh;
    }
    return y;
  }

  proc sign_l(m : M) = {
    var w, st, c, oz, j;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    j <- 0;
    oz <- None;
    while(oz = None /\ j < kappa) {
      (w, st) <$ commit sk;
      c <$ dC;
      oz <- respond sk st c;
      RO.set((w, m), c);
      j <- j + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }

  proc sign_r(m : M) = { 
    var w, st, c, oz, j;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    j <- 0;
    oz <- None;
    while(oz = None /\ j < kappa) {
      (w, st) <$ commit sk;
      c <$ dC;
      oz <- respond sk st c;
      RO.m <- (oz <> None) ? RO.m.[(w,m) <- c] : RO.m;
      j <- j + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }    

  proc sign(m : M) = {
    var r;
    r <- witness;
    if (countS < qs) {
      if (countS < i_) r <@ sign_l(m);
      else r <@ sign_r(m);
      countS <- countS + 1;
    }
    return r;
  }

}.

module Oij : Oracle_i_ij = {
  include var Oi [-init, sign]

  proc init(sk_i:SK, i_i:int, j_i:int) = { 
    i_ <- i_i;
    j_ <- j_i;
    j_end <- false;
    sk <- sk_i;
    countS <- 0;
    countH <- 0;
    wo <- None;
    w_ <- witness;
    logh <- [];
  }

  proc sign_i_l (m:M) = {
    var w, st, c, oz;
    (w, st) <$ commit sk;
    c <$ dC;
    oz <- respond sk st c;
    RO.set((w, m), c);
    return (w, c, oz);
  }

  proc sign_i_j (m:M) = {
    var st, c, oz;
    (w_, st) <$ commit sk;
    c <$ dC;
    oz <- respond sk st c;
    (* The !j_end and wo = None seems stupid but it is important to be able prove preservation of bads (for the two next bad event *)
    wo <- (!j_end /\ wo = None /\ oz = None) ? Some w_ : wo;  
    j_end <- true;
    RO.m <- (oz <> None) ? RO.m.[(w_,m) <- c] : RO.m;
    return (w_, c, oz);
  }

  proc sign_i_r (m:M) = {
    var w, st, c, oz;
    (w, st) <$ commit sk;
    c <$ dC;
    oz <- respond sk st c;
    RO.m <- (oz <> None) ? RO.m.[(w,m) <- c] : RO.m;
    return (w, c, oz);
  }

  proc sign_i (m:M): (W * C * Z) option = {
    var w, c, oz, j;
    w <- witness;
    c <- witness;
    j <- 0;
    oz <- None;
    while(oz = None /\ j < kappa) {
      if (j < j_) (w, c, oz) <@ sign_i_l (m);
      else { if (j_ = j) (w, c, oz) <@ sign_i_j (m); 
      else (w, c, oz) <@ sign_i_r (m); }
      j <- j + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }
  
  proc sign(m:M) = {
    var r;
    r <- witness;
    if (countS < qs) {
      if (countS < i_) r <@ sign_l(m);
      else { if (i_ = countS) r <@ sign_i(m);
      else r <@ sign_r(m); }
      countS <- countS + 1;
    }
    return r;
  }

}.

module Hi (Adv: Adversary) (O: Oracle_i_i) = {
  proc main(i_i:int) = {
    var b, pk, sk;
    (pk, sk) <$ keygen;
    RO.init();
    O.init(sk, i_i);
    b <@ Adv(O).distinguish(pk);
    return b;
  }
}.

module Hij (Adv: Adversary) (O: Oracle_i_ij) = {
  proc main(i_i:int, j_i:int) = {
    var b, pk, sk;
    (pk, sk) <$ keygen;
    RO.init();
    O.init(sk, i_i, j_i);
    b <@ Adv(O).distinguish(pk);
    return b;
  }
}.

section Analysis.

declare module Adv <: Adversary {-Count, -RO, -OLeft, -Oi}.

declare axiom A_ll : forall (O <: Oracle{-Adv}),
  islossless O.h =>
  islossless O.sign =>
  islossless Adv(O).distinguish.

declare axiom A_bound : forall (O <: Oracle_i{-Adv, -Count}), 
  hoare [ Adv(Count(O)).distinguish : Count.countS = 0 /\ Count.countH = 0 ==> 
                                      Count.countS <= qs /\ Count.countH <= qh ].

section C.

declare module O <: Oracle_i{-Adv, -Count}.

declare axiom Oh_ll : islossless O.h.
declare axiom Os_ll : islossless O.sign.

equiv Count_CountBound: 
  Adv(Count(O)).distinguish ~ Adv(CountBound(O)).distinguish : 
  ={arg} /\ ={glob Adv, glob Count, glob O} /\ Count.countS{1} = 0 /\ Count.countH{1} = 0 ==>
  ={res}.
proof.
  symmetry.
  conseq (: !(qs < Count.countS \/ qh < Count.countH){2} => ={arg, glob Adv, glob Count, glob O} ==>
            !(qs < Count.countS \/ qh < Count.countH){2} => ={res}) 
         _ ( A_bound O) => />; 1: smt(). 
  proc *.
  call (: qs < Count.countS \/ qh < Count.countH, ={glob Count, glob O}).
  + apply A_ll.
  + proc; sp 1 0; if{1}; 1: by wp; call(: true); auto.
    by call{2} Oh_ll; auto => /#.
  + by move=> *; islossless; apply Oh_ll.
  + by move=> _; proc; call Oh_ll; auto => /#.
  + proc; sp 1 0; if{1}; 1: by wp; call(: true); auto.
    by call{2} Os_ll; auto => /#.
  + by move=> *; islossless; apply Os_ll.
  + by move=> _; proc; call Os_ll; auto => /#.
  auto => /#.
qed.

lemma Pr_Count_CountBound &m : 
  Pr[Game(Adv, Count(O)).main() @ &m : res] =
  Pr[Game(Adv, CountBound(O)).main() @ &m : res].
proof.
  have -> : Pr[Game(Adv, Count(O)).main() @ &m : res] = Pr[GameI(Adv, Count(O)).main() @ &m : res].
  + byequiv => //; sim.
  byequiv => //; proc; call Count_CountBound. inline *; auto; sim />.
qed.

end section C.

local lemma Gleft_Hi &m : 
  Pr[Game(Adv, Count(OLeft)).main() @ &m : res] = 
  Pr[Hi(Adv, Oi).main(qs) @ &m : res].
proof.
  have -> := Pr_Count_CountBound OLeft.
  + islossless. 
  + islossless. 
    while true (kappa - i).
    + move=> z; wp; conseq (:true); 1: smt(); islossless.
    by auto => /#.
  byequiv => //; proc.
  call (: ={RO.m} /\ OLeft.sk{1} = Oi.sk{2} /\ Oi.i_{2} = qs /\ 
          Count.countS{1} = Oi.countS{2} /\ Count.countH{1} = Oi.countH{2}).
  + by sim />. 
  + proc; sp; if => //; rcondt{2} ^if; auto; sim />.
  inline *; auto.
qed.
  
local lemma Gright_Hi &m : 
  Pr[Game(Adv, Count(ORight)).main() @ &m : res] = 
  Pr[Hi(Adv, Oi).main(0) @ &m : res].
proof.
  have -> := Pr_Count_CountBound ORight.
  + islossless. 
  + islossless. 
    while true (kappa - i).
    + move=> z; wp; conseq (:true); 1: smt(); islossless.
    by auto => /#.
  byequiv => //; proc. 
  call (: ={RO.m} /\ OLeft.sk{1} = Oi.sk{2} /\ (Oi.i_ <= Oi.countS){2} /\
          Count.countS{1} = Oi.countS{2} /\ Count.countH{1} = Oi.countH{2}).
  + sim />.
  + proc; sp; if => //.
    rcondf{2} ^if; 1: by auto => /#.
    wp; conseq (: y{1} = r{2} /\ OLeft.sk{1} = Oi.sk{2} /\ ={RO.m}); 1: smt().
    inline *; wp.
    by while (={w, c, oz} /\ i{1} = j{2} /\ m{1} = m0{2} /\ OLeft.sk{1} = Oi.sk{2} /\
           RO.m{2} = (if oz <> None then RO.m.[(w, m) <- c] else RO.m){1}); auto.
  by inline *; auto.
qed.

local lemma Gleft_Gright_aux &m: 
  Pr[Game(Adv, Count(OLeft)).main() @ &m : res] - 
  Pr[Game(Adv, Count(ORight)).main() @ &m : res] = 
     bigi predT (fun (i : int) => Pr[Hi(Adv, Oi).main(i+1) @ &m : res] - Pr[Hi(Adv, Oi).main(i) @ &m : res]) 0 qs.
proof.
  rewrite Gright_Hi Gleft_Hi.
  by have /= -> := (hybrid_sum (fun i => Pr[Hi(Adv, Oi).main(i) @ &m : res]) 0 qs qs_ge0).
qed.

(* We focus on Pr[Hi(Adv, Oi).main(i) @ &m : res ] - Pr[Hi(Adv, Oi).main(i+1) @ &m : res] *)

section I.

declare op i : int.
declare axiom i_bounded : 0 <= i < qs.

local lemma Hi_Hij &m : 
   Pr[Hi(Adv, Oi).main(i) @ &m : res] = 
   Pr[Hij(Adv,Oij).main(i,0) @ &m : res].
proof.
  byequiv => //; proc.
  call (: ={Oi.i_, Oi.sk, Oi.countS, Oi.countH, RO.m} /\ Oi.j_{2} = 0).
  + by sim />.
  + proc; sp; if => //; wp; if => //; 1: by sim/>.
    if{2}; 2: by sim/>.
    inline Oi.sign_r Oij.sign_i; wp.
    while (={m0, w, c, j, oz, RO.m, Oi.sk} /\ Oi.j_{2} <= j{2}); last by auto.
    rcondf{2} ^if; 1: by auto => /#.
    by inline *; if{2}; auto => /> /#.
  by inline *; auto.   
qed.

local lemma Hi1_Hij &m : 
   Pr[Hi(Adv, Oi).main(i+1) @ &m : res] = 
   Pr[Hij(Adv,Oij).main(i,kappa) @ &m : res].
proof.
  byequiv => //; proc.
  call (: ={Oi.sk, Oi.countS, Oi.countH, RO.m} /\ Oi.i_{1} = Oi.i_{2} + 1 /\ Oi.j_{2} = kappa).
  + by sim />.
  + proc; sp; if => //; wp; if{2}.
    + rcondt{1} ^if; 1: by auto => /#.
      by sim />.
    if{2}; 2: by rcondf{1} ^if; [auto=> /# | sim />].
    rcondt{1} ^if; 1: by auto => /#.  
    inline Oi.sign_l Oij.sign_i Oij.sign_i_l; wp.
    while (={m0, w, c, oz, RO.m, Oi.sk, j} /\ Oi.j_{2} = kappa); 2: by auto.
    by inline RO.set; rcondt{2} ^if; auto.
  by inline *; auto.
qed.

local lemma HiHi1 &m : 
  Pr[Hi(Adv, Oi).main(i+1) @ &m : res] - Pr[Hi(Adv, Oi).main(i) @ &m : res] =
  bigi predT (fun (j : int) => Pr[Hij(Adv, Oij).main(i,j+1) @ &m : res] - Pr[Hij(Adv, Oij).main(i, j) @ &m : res]) 0 kappa.
proof.
  rewrite Hi_Hij Hi1_Hij.
  have /= -> // := (hybrid_sum (fun j => Pr[Hij(Adv, Oij).main(i, j) @ &m : res]) 0 kappa).
  smt(kappa_gt0).
qed.

(* We focus on Pr[Pr[Hij(Adv, Oij).main(i,j+1) @ &m : res] - Pr[Hij(Adv, Oij).main(i, j) @ &m : res] *)
section J.

declare op j : int.
declare axiom j_bounded : 0 <= j < kappa.

op is_update (m1 m2: (W * M, C)fmap) (w:W) = 
  exists m c, m1 = m2.[(w,m) <- c].

lemma is_update_set m1 m2 w k c: is_update m1 m2 w => is_update m1.[k<-c] m2.[k<-c] w.
proof.
  move=> /> m c'; case: (k = (w, m)) => [-> | ? ]; [exists m c | exists m c']; 
    apply fmap_eqP; smt(get_setE).
qed.

local lemma Oij_sign_ll : islossless Oij.sign.
proof.
  islossless. 
  + while true (kappa - j); 2: by auto => /#.
    by move=> z; wp; conseq (:true); 1: smt(); islossless.
  + while true (kappa - j); 2: by auto => /#.
    by move=> z; wp; conseq (:true); 1: smt(); islossless.
  while true (kappa - j); 2: by auto => /#.
  by move=> z; wp; conseq (:true); 1: smt(); islossless.
qed.

local lemma bad &m : 
  Pr[Hij(Adv, Oij).main(i,j+1) @ &m : res] <= Pr[Hij(Adv, Oij).main(i, j) @ &m : res] + 
  Pr[Hij(Adv, Oij).main(i, j) @ &m : exists w, w \in Oi.logh /\ Oi.wo = Some w].
proof.
  byequiv (: _ ==> !(exists w, w \in Oi.logh /\ Oi.wo = Some w){2} => ={res}) => //; last first.
  + by move=> &1 &2 h1 /h1 ->.
  proc.
  call (: (exists w, w \in Oi.logh /\ Oi.wo = Some w),
          (Oi.countS <= Oi.i_ => !Oi.j_end /\ Oi.wo = None){2} /\
          (forall w, w \in Oi.logh => exists m, (w, m) \in RO.m){2} /\
          ={Oi.countS, Oi.countH, Oi.i_, Oi.sk, Oi.logh} /\ Oi.j_{1} = Oi.j_{2} + 1 /\ 0 <= Oi.j_{2} < kappa/\
          if Oi.wo{2} = None then ={RO.m}
          else is_update RO.m{1} RO.m{2} (oget Oi.wo{2})).
  + apply A_ll.
  + by proc; inline *; sp; if => //; auto => |>; smt(is_update_set get_setE).
  + by move=> *; islossless.
  + by move=> _; proc; inline *; sp; if => //; auto => />; smt( dC_ll get_setE).
  + proc; sp; if => //; wp.
    if => //.
    + conseq (: _ ==> ={RO.m, r} /\ (forall w, w \in Oi.logh => exists m, (w, m) \in RO.m){2}); 1: smt().
      inline *; wp.
      while (={m0, w, c, j, oz, RO.m, Oi.sk}  /\ (forall w, w \in Oi.logh => exists m, (w, m) \in RO.m){2}).
      + by auto => />; smt(get_setE).
      by auto => /> /#.
    if => //; last first.
    + inline *; wp.
      while(={m0,w,c,j,oz, Oi.sk} /\ 
            (forall w, w \in Oi.logh => exists m, (w, m) \in RO.m){2} /\
            if Oi.wo{2} = None then ={RO.m}
            else is_update RO.m{1} RO.m{2} (oget Oi.wo{2})).
      + by auto => |>; smt(is_update_set get_setE).
      by auto => |> /#. 
    inline Oij.sign_i RO.set; wp.
    while( (j{1} <= Oi.j_{2} => !Oi.j_end{2} /\ Oi.wo{2} = None) /\     
              ={Oi.countS, Oi.i_, Oi.sk, j, m0, c, oz, w} /\ Oi.j_{1} = Oi.j_{2} + 1 /\ 0 <= Oi.j_{2} /\
             (forall w, w \in Oi.logh => exists m, (w, m) \in RO.m){2} /\
             if (j <= Oi.j_ \/ Oi.wo = None){2} then ={RO.m} else is_update RO.m{1} RO.m{2} (oget Oi.wo{2})).
    + wp; if{2}.
      + rcondt{1} ^if; 1: by auto => /#.
        by inline *; auto => |>; smt(get_setE).
      if{2}; last first.
      + rcondf{1} ^if; 1: by auto => /#.
        if{1}; inline *; auto => |> *; smt(get_setE is_update_set).
      rcondt{1} ^if; 1: by auto => /#.
      by inline *; auto => />; smt(is_update_set get_setE).
    by auto => /> /#.
  + by move=> _ _; apply Oij_sign_ll.
  + move=> _. 
    conseq Oij_sign_ll 
      (:(exists (w : W), (w \in Oi.logh) /\ Oi.wo = Some w) ==> 
        (exists (w : W), (w \in Oi.logh) /\ Oi.wo = Some w)) => //; 1: smt(). 
    proc; sp; if => //; 2: by auto => />.
    inline *; wp; if.
    + wp; while (exists (w4 : W), (w4 \in Oi.logh) /\ Oi.wo = Some w4); auto; smt(get_setE).
    if; wp; while (exists (w4 : W), (w4 \in Oi.logh) /\ Oi.wo = Some w4); auto; 2..4: smt(get_setE).
    if; 1: by auto; smt(get_setE).
    if; auto; smt(get_setE).
  inline *; auto; smt (j_bounded).
qed.

local module Oij_split : Oracle_i_ij = {
  import var Oi
  include Oij [-sign, sign_i]

  proc sign_i (m:M): (W * C * Z) option = {
    var w, c, oz, j;
    w <- witness;
    c <- witness;
    j <- 0;
    oz <- None;
    while(oz = None /\ j < j_) {
      (w, c, oz) <@ sign_i_l (m);
      j <- j + 1;
    }
    j <- j_; (* this is ensured if oz = None *)
    if (oz = None /\ j_ < kappa) (w, c, oz) <@ sign_i_j (m); 
    j <- j_ + 1;
    while (oz = None /\ j < kappa) {
       (w, c, oz) <@ sign_i_r (m); 
      j <- j + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }

  proc sign(m:M) = {
    var r;
    r <- witness;
    if (countS < qs) {
      if (countS < i_) r <@ sign_l(m);
      else { if (i_ = countS) r <@ sign_i(m);
      else r <@ sign_r(m); }
      countS <- countS + 1;
    }
    return r;
  }

}.

local lemma Oij_Oij_split &m : 
  Pr[Hij(Adv, Oij).main(i, j) @ &m : exists w, w \in Oi.logh /\ Oi.wo = Some w] = 
  Pr[Hij(Adv, Oij_split).main(i, j) @ &m : exists w, w \in Oi.logh /\ Oi.wo = Some w].
proof.
  byequiv => //; proc.
  call (: ={glob Oi} /\ 0 <= Oi.j_{2} < kappa); last by inline *; auto; smt(j_bounded).
  + by sim />.
  proc; sp; if => //.
  if => //; 1: by sim />.
  if => //; 2: by sim />.
  inline Oij.sign_i Oij_split.sign_i.
  sim />.
  splitwhile{1} ^while : (j < Oi.j_). 
  seq 6 6 : (={glob Oi, m0, w, c, j, oz} /\ 
              (0 <= Oi.j_ < kappa /\ (oz = None => j = Oi.j_)){2}).
  + while (={glob Oi, m0, w, c, j, oz} /\ (j <= Oi.j_ /\ 0 <= Oi.j_ < kappa){2}).
    + rcondt{1} ^if; 1: by auto. 
      by wp; conseq (: ={glob Oi, w, c, oz}); [smt() | sim].
    by auto => /> /#.
  sp; if{2}.
  + rcondt{1} ^while; 1: by auto => /#.
    rcondf{1} ^if; 1: by auto => /#.
    rcondt{1} ^if; 1: by auto => /#.
    while (={glob Oi, m0, w, c, j, oz} /\ (Oi.j_ < j){2}).
    + rcondf{1} ^if; 1: by auto => /#.
      rcondf{1} ^if; 1: by auto => /#.
      by wp; conseq (: ={RO.m, w, c, oz}); [ smt() | sim].
    wp; conseq (: ={RO.m, w, c, oz, Oi.wo, Oi.w_, Oi.j_end} /\ j{1} = Oi.j_{2}); 1: smt().
    by sim => />.
  rcondf{2} ^while; 1: by auto => />.
  by rcondf{1} ^while; auto => />.
qed.

local module Oij_bad : Oracle_i_ij = {
  import var Oi
  include var Oij [-sign, sign_i, sign_i_j]

  proc sign_i_j (m:M) : W * C * Z option = {
    var st, c;
    (w_, st) <$ commit sk;
    c <$ dC;
    j_end <- true;
    return (witness, c, None);
  }

 proc sign_i (m:M): (W * C * Z) option = {
    var w, c, oz, j;
    w <- witness;
    c <- witness;
    j <- 0;
    oz <- None;
    while(oz = None /\ j < j_) {
      (w, c, oz) <@ sign_i_l (m);
      j <- j + 1;
    }
    j <- j_; (* this is ensured if oz = None *)
    if (oz = None /\ j_ < kappa) (w, c, oz) <@ sign_i_j (m); 
    j <- j_ + 1;
    while (oz = None /\ j < kappa) {
       (w, c, oz) <@ sign_i_r (m); 
      j <- j + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }

  proc sign(m:M) = {
    var r;
    r <- witness;
    if (countS < qs) { 
      if (countS < i_) r <@ sign_l(m);
      else { if (i_ = countS) r <@ sign_i(m);
      else r <@ sign_r(m); }
      countS <- countS + 1;
    }
    return r;
  }

}.

local lemma Oij_split_Oij_bad &m : 
   Pr[Hij(Adv, Oij_split).main(i, j) @ &m : exists w, w \in Oi.logh /\ Oi.wo = Some w] <= 
   Pr[Hij(Adv, Oij_bad).main(i, j) @ &m : Oi.j_end /\ Oi.w_ \in Oi.logh].
proof. 
  byequiv => //; symmetry; proc.
  call (: Oi.j_end /\ Oi.wo = None,
          (Oi.countS <= Oi.i_ => !Oi.j_end /\ Oi.wo = None){2} /\ 
          ={Oi.logh, RO.m, Oi.sk, Oi.countS, Oi.countH, Oi.i_, Oi.j_, Oi.j_end, Oi.w_} /\ 
          0 <= Oi.j_{1} < kappa /\ 
          (Oi.wo <> None => Oi.j_end /\ Oi.wo = Some Oi.w_){2}).
  + apply A_ll.
  + by proc; sp; if => //; inline *; auto.
  + by move=> *; islossless.
  + by move=> _; proc; sp; if => //; inline *; auto => />; smt( dC_ll get_setE).    
  + proc; sp; if => //; wp; if => //.
    + by conseq (: ={r, RO.m}); 1: smt(); sim.
    if => //; wp; last by conseq (: ={r, RO.m}); 1: smt(); sim.
    inline Oij_bad.sign_i Oij_split.sign_i.
    seq 7 7 : 
       ((!Oi.j_end /\ Oi.wo = None){2} /\ 0 <= Oi.j_{1} < kappa /\ Oi.i_{1} = Oi.countS{1} /\
         ={Oi.logh, RO.m, Oi.sk, Oi.countS, Oi.countH, Oi.i_, Oi.j_, Oi.j_end, Oi.w_, m0, w, c, j, oz}).
    + by conseq (: ={RO.m, m0, w, c, j, oz}); [ smt() | sim].
    inline Oij_bad.sign_i_j Oij_split.sign_i_j.
    seq 2 2 : ( !(Oi.j_end{2} /\ Oi.wo{2} = None) =>
                    (={Oi.logh, RO.m, Oi.sk, Oi.countS, Oi.countH, Oi.i_, Oi.j_, Oi.j_end, Oi.w_, m0, c, j, oz} /\  
                     (Oi.j_end{2} => oz{1} = None) /\ 0 <= Oi.j_{1} < kappa /\
                     (Oi.wo{2} <> None => Oi.j_end{2} /\ Oi.wo{2} = Some Oi.w_{2}) /\
                     Oi.i_{1} = Oi.countS{1}) /\
                     (oz{1}<> None => ={w})).
    + by if; auto => /> /#.
    (* case on if bad is set *)
    case: (Oi.j_end{2} /\ Oi.wo{2} = None).
    + conseq (: true) => />.
      wp; while{1} true (kappa-j{1}).
      + by move=> _ z; wp; conseq (:true); [smt() | islossless].
      while{2} true (kappa-j{2}); last by auto => /#.
      by move=> _ z; wp; conseq (:true); [smt() | islossless].
    wp; while (={j, oz, c, m0, RO.m, Oi.sk} /\ (oz{1} <> None => ={w})).
    + by inline *; auto => /> /#.
    by skip => /> /#.
  + move=> *; islossless.
    + while true (kappa - j); 2: by auto => /#.
      by move=> z; wp; conseq (:true); 1: smt(); islossless.
    + while true (kappa - j); 2: by auto => /#.
      by move=> z; wp; conseq (:true); 1: smt(); islossless.
    + while true (Oi.j_ - j); 2: by auto => /#.
      by move=> z; wp; conseq (:true); 1: smt(); islossless.
    while true (kappa - j); 2: by auto => /#.
    by move=> z; wp; conseq (:true); 1: smt(); islossless.
  + move=> _; proc; sp; if => //; wp; if.
    + conseq />; islossless.
      while true (kappa - j); 2: by auto => /#.
      by move=> z; wp; conseq (:true); 1: smt(); islossless.
    if; last first.
    + conseq />; islossless.
      while true (kappa - j); 2: by auto => /#.
      by move=> z; wp; conseq (:true); 1: smt(); islossless.
    inline *; wp.
    conseq (: true ==> true) (: Oi.j_end /\ Oi.wo = None ==> Oi.j_end /\ Oi.wo = None) => //.
    + while true => //.
      wp; seq 7 : (Oi.j_end /\ Oi.wo = None) => //.
      + by conseq />.
      by if; auto => /#.
    islossless.
    + while true (kappa - j); 2: by auto => /#.
      by move=> z; wp; conseq (:true); 1: smt(); islossless.
    while true (Oi.j_ - j); 2: by auto => /#.
    by move=> z; wp; conseq (:true); 1: smt(); islossless.
  inline *; auto => />; smt(j_bounded).
qed.

local clone PROM.FullRO as ROsk with
  type in_t <= SK, 
  type out_t <= W * ST,
  op dout <= commit,
  type d_in_t <= int * int,
  type d_out_t <= bool
proof *.

local module Oij_LE (H : ROsk.RO) : Oracle_i_ij = {
  import var Oi
  include var Oij [-sign, sign_i, sign_i_j]

  (* i_done is used when we do fel *)
  var i_done : bool

  proc sign_i (m:M): (W * C * Z) option = {
    var w, c, oz, j;
    if (!i_done /\ valid_sk Oi.sk) {
    w <- witness;
    c <- witness;
    j <- 0;
    oz <- None;
    while(oz = None /\ j < Analysis.j) {
      (w, c, oz) <@ sign_i_l (m);
      j <- j + 1;
    }
    j <- j_; (* this is ensured if oz = None *)
    if (oz = None /\ j_ < kappa) { 
        H.sample(sk);
        c <$ dC;
        j_end <- true;
        w <- witness;
        oz <- None;
    }
    j <- j_ + 1;
    while (oz = None /\ j < kappa) {
       (w, c, oz) <@ sign_i_r (m); 
      j <- j + 1;
    }
    i_done <- true;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }

  proc sign(m:M) = {
    var r;
    r <- witness;
    if (countS < qs) {
      if (countS < i_) r <@ sign_l(m);
      else { if (i_ = countS) r <@ sign_i(m);
      else r <@ sign_r(m); }
      countS <- countS + 1;
    }
    return r;
  }
}.

local module D (H :  ROsk.RO) = {

  proc distinguish1 (i_i:int, j_i:int) = {
    var b, pk, sk;
    (pk, sk) <$ keygen; Oij_LE.i_done <- false;
    RO.init();
    H.init();
    Oij_LE(H).init(sk, i_i, j_i);
    b <@ Adv(Oij_LE(H)).distinguish(pk);
  }

  proc distinguish (i_i:int, j_i:int) = {
    var st;
    distinguish1 (i_i, j_i);
    (Oi.w_, st) <@ H.get(Oi.sk);
    return (Oi.j_end /\ b2i Oij_LE.i_done <= 1) /\ Oi.w_ \in Oi.logh;
  }

}.

local lemma Oij_bad_DRO &m : 
  Pr[Hij(Adv, Oij_bad).main(i, j) @ &m : Oi.j_end /\ Oi.w_ \in Oi.logh] = 
  Pr[D(ROsk.RO).distinguish(i, j) @ &m : res].
proof.
  byequiv (: ={glob Adv, arg} /\ i_i{1} = i /\ j_i{1} = j ==> _) => //.
  proc; wp; inline D(ROsk.RO).distinguish1; sp 0 2.
  seq 4 6: (={Oi.j_end, Oi.logh} /\
             (Oi.j_end => exists st, ROsk.RO.m.[Oi.sk] = Some (Oi.w_{1}, st)){2});
    last first.
  + case: (Oi.j_end{2}).
    + by inline *; rcondf{2} ^if; auto => /> /#.
    by call{2} (:true ==> true); [islossless | auto].
  call (: ={RO.m, Oi.j_end, Oi.j_, Oi.logh, Oi.countH, Oi.countS, Oi.countH, Oi.sk, Oi.i_} /\
          Oi.j_{2} = Analysis.j /\ valid_sk Oi.sk{2} /\
          (Oi.countS <= Oi.i_ => (!Oij_LE.i_done /\ !Oi.j_end /\ Oi.sk \notin ROsk.RO.m)){2} /\
          (Oi.j_end => exists st, ROsk.RO.m.[Oi.sk] = Some (Oi.w_{1}, st)){2}).
  + by sim />.
  + proc; sp; if => //; wp; if => //; 2: if => //; last first.
    + by conseq (: ={r, RO.m}); [smt() | sim].
    + by conseq (: ={r, RO.m}); [smt() | sim].
    inline Oij_bad.sign_i Oij_LE(ROsk.RO).sign_i.
    rcondt{2} ^if; 1: auto => /> /#.
    wp; while (={RO.m, m0, oz, w, c, j, Oi.sk}).
    + by inline *; auto.
    wp; seq 7 7 :
     (={RO.m, m0, oz, w, c, j, Oi.sk, Oi.j_end, Oi.j_, Oi.logh, Oi.countH, Oi.countS, Oi.sk, Oi.i_} /\
     Oi.j_{2} = Analysis.j /\ valid_sk Oi.sk{2} /\
     (!Oi.j_end /\  Oi.sk \notin ROsk.RO.m /\ Oi.countS = Oi.i_){2}).
    + conseq (: ={RO.m, m0, oz, w, c, j, Oi.sk, Oi.j_end, Oi.j_, Oi.logh, Oi.countH, Oi.countS, Oi.sk, Oi.i_}); 1: smt().
      wp; while (={RO.m, m0, oz, w, c, j, Oi.sk} /\  Oi.j_{1} = Analysis.j); last by auto.
      by conseq (: ={RO.m, oz, w, c, j}) => />; sim.
    if=> //; 2: auto => /> /#.
    by inline *; rcondt{2} ^if; auto => />; smt(get_setE).
  by inline *; auto => /> *; rewrite mem_empty /= /#.
qed.

local lemma DRO_DLRO &m : 
  Pr[D(ROsk.RO).distinguish(i, j) @ &m : res] = 
  Pr[D(ROsk.LRO).distinguish(i, j) @ &m : res].
proof. byequiv (ROsk.FullEager.RO_LRO_D D commit_ll) => //. qed.

(* MOVE this to EC *)
lemma mu_dlet_le (d : 'a distr) (F1 : 'a -> 'b distr) (F2 : 'a -> 'c distr) P1 P2 :
  (forall x, mu (F1 x) P1 <= mu (F2 x) P2) => 
  mu (dlet d F1) P1 <= mu (dlet d F2) P2.
proof.
  rewrite !dletE => h; apply ler_sum_pos => /=; 1: smt(mu_bounded).
  by apply (summable_le_pos _ (mu1 d)); [ apply summable_mu1 | smt(mu_bounded)].
qed.

local lemma phr_j_end:
  phoare [D(ROsk.LRO).distinguish1 : i_i = i /\ j_i = j ==> Oi.j_end ] <= (p ^ Analysis.j).
proof. 
  conseq (: Oi.j_end /\ b2i (Oij_LE.i_done) <= 1); 1: smt(). 
  bypr => &m *.
  fel 5 (b2i (Oij_LE.i_done)) (fun _ => p ^ j) 1 Oi.j_end
      [Oij_LE(ROsk.LRO).sign_i : (!Oij_LE.i_done /\ valid_sk Oi.sk)]
      true => //.
  + by have /= -> := big_int1 0.
  + by inline *; auto.
  + proc; rcondt^if => //.
    seq 6 : (oz = None) (p^Analysis.j) 1%r _ 0%r (!Oi.j_end) => //.
    + by conseq /> => //.
    + sp; wp; conseq (: _ : <= (if oz = None then p ^ (Analysis.j - j) else 0%r)) => //.
      while (j <= Analysis.j /\ valid_sk Oi.sk).
      + move=> hw; exlim j => j0.
        conseq (: _ : <= (if oz = None then p ^ (Analysis.j - j0) else 0%r)) => //.
        seq 2 : (oz = None) p (p^(Analysis.j - (j0 + 1))) _ 0%r (j = j0 + 1 /\ j <= Analysis.j /\ valid_sk Oi.sk).
        + by wp; conseq (:true) => // /#.
        + wp; inline *; wp 4.
          rndsem 1.
          rnd (fun (wscoz: _ * _ * _ * _) => wscoz.`4 = None).
          auto => |> &hr _ hv _; apply (RealOrder.ler_trans _ _ _ _ (p_def Oi.sk{hr} hv)).
          apply mu_dlet_le => w_st /=; apply mu_dlet_le => c.
          by rewrite dunitE dunit1E.
        + by conseq hw => />. 
        + by conseq hw => /#.
        move=> /> *; rewrite -RField.exprS /#. 
      by auto => />; smt (RField.expr0 j_bounded).
    by rcondf^if; 1: auto => />; conseq />.
  + move=> c; proc; rcondt^if => //; wp.
    by conseq (:true) => />; auto.
  by move=> b c; proc; rcondf^if => //.
qed.

local lemma Pr_DLRO &m : Pr[D(ROsk.LRO).distinguish(i, j) @ &m : res] <= p^j * (qh%r * eps).
proof.
  (* the eps should be easy the difficult par is p^j *)
  byphoare ( : i_i = i /\ j_i = j ==> res) => //; proc.
  seq 1 : Oi.j_end (p ^ j) (qh%r * eps) _ 0%r (Oi.sk \notin ROsk.RO.m /\ size Oi.logh <= qh /\ valid_sk Oi.sk) => //;
    last by conseq (:false) => />.  
  + inline D(ROsk.LRO).distinguish1.
    call (: Oi.sk \notin ROsk.RO.m /\ size Oi.logh <= Oi.countH <= qh /\ valid_sk Oi.sk).
    + by proc; sp; if => //; wp; conseq (:true) => // /#. 
    + by conseq />.
    inline *; auto => />; smt(qh_ge0 mem_empty).  
  (* [ : Oi.j_end ] <= p ^ j *)
  + by call (phr_j_end); auto => />.
  (* [w_ <@ dfst (commit sk) : w_ \in logh ] *)
  inline *; rcondt ^if; 1: by auto.
  wp; rnd (fun x => fst x \in Oi.logh); auto => |> &hr _ hsize hval _; split; 2: smt(get_setE).
  have := mu_mem_le_mu1 (dfst (commit Oi.sk{hr})) Oi.logh{hr} eps (valid_sk_mu1_commit Oi.sk{hr} hval).
  rewrite /dfst dmapE /(\o); smt (eps_ge0 size_ge0).
qed.

lemma concl_j &m: 
  Pr[Hij(Adv, Oij).main(i,j+1) @ &m : res] - Pr[Hij(Adv, Oij).main(i, j) @ &m : res] <=  p^j * (qh%r*eps).
proof.
  have := bad &m; have := Oij_split_Oij_bad &m; have := Pr_DLRO &m.
  rewrite (Oij_Oij_split &m) (Oij_bad_DRO &m) (DRO_DLRO &m) /#.
qed.

end section J.

lemma concl_i &m : 
  Pr[Hi(Adv, Oi).main(i+1) @ &m : res] - Pr[Hi(Adv, Oi).main(i) @ &m : res] <=
    (qh%r * eps) / (1%r - p).
proof.
  apply (RealOrder.ler_trans (bigi predT (fun (j : int) =>  p^j * (qh%r * eps)) 0 kappa)).
  + rewrite (HiHi1 &m); apply StdBigop.Bigreal.ler_sum_seq => j /mem_range hj _ /=.
    by apply concl_j.
  rewrite -mulr_suml; have := sum_pow_le p kappa _ _; smt(kappa_gt0 p_bounded qh_ge0 eps_ge0).
qed.

end section I.

lemma Gleft_Gright &m: 
  Pr[Game(Adv, Count(OLeft)).main() @ &m : res] - 
  Pr[Game(Adv, Count(ORight)).main() @ &m : res] <= 
     qs%r * (qh%r * eps) / (1%r - p).
proof.
  apply (RealOrder.ler_trans (bigi predT (fun (j : int) =>  (qh%r * eps)/(1%r - p)) 0 qs)).
  rewrite Gleft_Gright_aux; apply StdBigop.Bigreal.ler_sum_seq => i /mem_range hi _ /=.
  apply (concl_i i hi).
  rewrite StdBigop.Bigreal.sumri_const 1:qs_ge0 //.
qed.

end section Analysis.

end Analysis.

(* To remove the dependency to Oi *)
section.

declare module Adv <: Adversary {-Count, -RO, -OLeft}.

declare axiom A_ll : forall (O <: Oracle{-Adv}),
  islossless O.h =>
  islossless O.sign =>
  islossless Adv(O).distinguish.

declare axiom A_bound : forall (O <: Oracle_i{-Adv, -Count}), 
  hoare [ Adv(Count(O)).distinguish : Count.countS = 0 /\ Count.countH = 0 ==> 
                                      Count.countS <= qs /\ Count.countH <= qh ].

local clone import Analysis as A.

lemma Gleft_Gright &m: 
  Pr[Game(Adv, Count(OLeft)).main() @ &m : res] - 
   Pr[Game(Adv, Count(ORight)).main() @ &m : res] <= 
  qs%r * (qh%r * eps) / (1%r - p).
proof. apply (A.Gleft_Gright Adv A_ll A_bound &m). qed.

end section.
