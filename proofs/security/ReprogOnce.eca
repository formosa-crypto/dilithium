require import AllCore Distr DBool PROM List.
require import Dexcepted.
require import Dfilter.
require import Real RealSeries.
require import DistrExtras.
require GuessReprog.
import Biased.
import SmtMap.

(* Define some necessary abstract stuff *)

type M, W, C, Z, ST, PK, SK.

op [lossless] keygen : (PK * SK) distr.
op [lossless] commit : SK -> (W * ST) distr.
op [lossless uniform] dC : C distr.
op respond : SK -> ST -> C -> Z option.

op valid_sk sk =
  exists pk, (pk, sk) \in keygen.

(* Needed to ensure various conditional distrs are well-formed. *)

axiom all_sk_can_accept sk :
  valid_sk sk =>
  exists w st c,
  (w, st) \in commit sk /\
  c \in dC /\
  respond sk st c <> None.

axiom all_sk_can_reject sk :
  valid_sk sk =>
  exists w st c,
  (w, st) \in commit sk /\
  c \in dC /\
  respond sk st c = None.

op alpha : real. (* pmax *)

(* CD: dtrans? - with follow up renamings *)
(* Transcript distribution *)
op dWCoZ sk : (W * C * Z option) distr =
  dlet (commit sk) (fun wst =>
  let (w, st) = wst in
  dlet dC (fun c =>
  let z = respond sk st c in
  dunit (w, c, z))).

(* Transcript distribution, conditioned on accept or reject. *)
(* CD: is the if-then-else really necessary? *)
(* Ethan: TODO Remove this if-then-else *)
op dWCoZ_rej sk = 
  if valid_sk sk then
    dcond (dWCoZ sk) (fun wcz => let (w, c, z) = wcz in z = None)
  else
    dunit witness.

op dWC_rej sk = 
  dmap (dWCoZ_rej sk) (fun wcz => let (w, c, _) = wcz in (w, c)).

op dXY_rej (k: SK * M) : ((W * M) * C) distr =
  let (sk, m) = k in
  dmap (dWC_rej sk) (fun wc =>
    let (w, c) = wc in ((w, m), c)).

axiom dX_pmax k :
  p_max (dfst (dXY_rej k)) <= alpha.

 (* max. number of signing queries *)
op qs : {int | 0 < qs} as qs_gt0.
(* max. number of hash queries *)
op qh : {int | 0 < qh} as qh_gt0.
(* max. number of iterations *)
op kappa : {int | 0 < kappa} as kappa_gt0.

clone import PROM.FullRO as RO with
  type in_t <= W * M,
  type out_t <= C,
  op dout <= (fun _ => dC)
proof *.

module type Oracle = {
  proc h(w: W, m: M) : C
  proc sign(m: M) : (W * C * Z) option
}.

module type Oracle_i = {
  include Oracle
  proc init(sk_i : SK) : unit
}.

module Count (O: Oracle_i) : Oracle = {
  var countS : int
  var countH : int

  proc init = O.init

  proc h(w: W, m : M) : C = {
    var y;
    countH <- countH + 1;
    y <@ O.h((w, m));
    return y;
  }

  proc sign(x) = {
    var y;
    countS <- countS + 1;
    y <@ O.sign(x);
    return y;
  }
}.

module OLeft : Oracle_i = {
  var sk : SK

  proc init(sk_i: SK) = {
    sk <- sk_i;
  }

  proc h = RO.get

  proc sign(m: M) = {
    var w, st, c, oz, i;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    i <- 0;
    oz <- None;
    while(oz = None /\ i < kappa) {
      (w, st) <$ commit sk;
      c <$ dC;
      oz <- respond sk st c;
      RO.set((w, m), c);
      i <- i + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }
}.

(* Moving RO.set outside while-loop *)
module ORight : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, st, c, oz, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    i <- 0;
    oz <- None;
    while(oz = None /\ i < kappa) {
      (w, st) <$ commit sk;
      c <$ dC;
      oz <- respond sk st c;
      i <- i + 1;
    }
    if(oz <> None)
      RO.set((w, m), c);
    return if oz <> None then Some (w, c, oget oz) else None;
  }
}.

module type Adversary(O: Oracle) = {
  proc distinguish(pk: PK): bool
}.

module Game (Adv: Adversary) (O: Oracle_i) = {
  proc main() = {
    var b, pk, sk;
    (pk, sk) <$ keygen;
    RO.init();
    O.init(sk);
    b <@ Adv(O).distinguish(pk);
    return b;
  }
}.

section Analysis.

declare module Adv <: Adversary {-Count, -RO, -OLeft}.

declare axiom A_bound : forall (O <: Oracle_i{-Adv, -Count}), 
  hoare [ Adv(Count(O)).distinguish : Count.countS = 0 /\ Count.countH = 0 ==> 
                                      Count.countS <= qs /\ Count.countH <= qh ].

declare axiom A_ll : forall (O <: Oracle{-Adv}),
  islossless O.h =>
  islossless O.sign =>
  islossless Adv(O).distinguish.

(* -- Ingredients required for cloning ReprogRej -- *)

op dWCoZ_acc sk = 
  if valid_sk sk then
    dcond (dWCoZ sk) (fun wcz => let (w, c, z) = wcz in z <> None)
  else
    dunit witness.

op dWCZ_acc sk = 
  dmap (dWCoZ_acc sk) (fun wcoz => let (w, c, oz) = wcoz in (w, c, oget oz)).

op dXYZ_acc (k: SK * M) : ((W * M) * C * Z) distr =
  let (sk, m) = k in
  dmap (dWCZ_acc sk) (fun wcz =>
    let (w, c, z) = wcz in ((w, m), c, z)).

lemma dWCoZ_can_acc sk :
  valid_sk sk =>
  0%r <
  mu (dWCoZ sk)
  (fun (wcz : W * C * Z option) => let (w, c, z) = wcz in z <> None).
proof.
move => H; apply all_sk_can_accept in H; case H => w st c [?[??]].
apply witness_support; exists (w, c, respond sk st c) => /> /=.
apply supp_dlet; exists (w, st) => />.
apply supp_dlet; exists c => />.
apply supp_dunit => /#.
qed.

lemma dWCoZ_can_rej sk :
  valid_sk sk =>
  0%r <
  mu (dWCoZ sk)
  (fun (wcz : W * C * Z option) => let (w, c, z) = wcz in z = None).
proof.
move => H; apply all_sk_can_reject in H; case H => w st c [?[??]].
apply witness_support; exists (w, c, None) => /=.
apply supp_dlet; exists (w, st) => />.
apply supp_dlet; exists c => />.
apply supp_dunit => /#.
qed.

lemma dXYZ_acc_ll k :
  is_lossless (dXYZ_acc k).
proof.
case k => sk m.
case (valid_sk sk) => H.
- rewrite /dXYZ_acc /= dmap_ll dmap_ll.
  rewrite /dWCoZ_acc H /= dcond_ll.
  exact dWCoZ_can_acc.
- rewrite /dXYZ_acc /= dmap_ll dmap_ll.
  rewrite /dWCoZ_acc H /=.
  exact dunit_ll.
qed.

lemma dXY_rej_ll k :
  is_lossless (dXY_rej k).
proof.
case k => sk m.
case (valid_sk sk) => H.
- rewrite /dXY_rej /= dmap_ll dmap_ll.
  rewrite /dWCoZ_rej H /= dcond_ll.
  exact dWCoZ_can_rej.
- rewrite /dXY_rej /= dmap_ll dmap_ll.
  rewrite /dWCoZ_rej H /=.
  exact dunit_ll.
qed.

local clone GuessReprog as Guess with
  type K <- SK * M,
  type X <- W * M,
  type Y <- C,
  type Z <- Z,
  op dXYZ_acc <- dXYZ_acc,
  op dXY_rej <- dXY_rej,
  op dY <- dC,
  op qh <- qh,
  op qr <- kappa * qs,
  op alpha <- alpha,
  theory RO <- RO
proof *.
realize dXYZ_acc_ll by apply dXYZ_acc_ll.
realize dXY_rej_ll by apply dXY_rej_ll.
realize dY_ll by apply dC_ll.
realize dY_uni by apply dC_uni.
realize dX_pmax by apply dX_pmax.
realize qh_gt0 by apply qh_gt0.
realize qr_gt0 by smt(kappa_gt0 qs_gt0).

(* Auxiliary lemma *)

lemma sum_over_bool (f : bool -> real):
  sum (fun b => f b) = f true + f false.
proof.
rewrite (sumE_fin _ [true; false]) /#.
qed.

local lemma dWCoZ_ll sk :
  is_lossless (dWCoZ sk).
proof.
apply dlet_ll; first by apply commit_ll.
move => x; case x; move => * /=; apply dlet_ll; first apply dC_ll.
move => * /=; apply dunit_ll.
qed.

(* Accept and reject probabilities *)
op p_acc sk = mu (dWCoZ sk) (fun wcz => let (w, c, oz) = wcz in oz <> None).
op p_rej sk = mu (dWCoZ sk) (fun wcz => let (w, c, oz) = wcz in oz = None).

(* Properties of distributions above *)

local lemma dWC_rej_ll sk :
  valid_sk sk =>
  is_lossless (dWC_rej sk).
proof.
move => H; apply dmap_ll.
rewrite /dWCoZ_rej H /=; apply dcond_ll.
by apply dWCoZ_can_rej => //.
qed.

(* CD: are these necessary? can you just use smt in place of them? *)
local lemma dWCoZ_acc_supp sk w c oz :
  valid_sk sk =>
  (w, c, oz) \in dWCoZ_acc sk =>
  oz <> None.
proof. smt(dcond_supp). qed.

local lemma dWCoZ_rej_None sk w c oz :
  valid_sk sk =>
  (w, c, oz) \in dWCoZ_rej sk =>
  oz = None.
proof. smt(dcond_supp). qed.

local lemma dWCoZ_rej_supp sk w c :
  valid_sk sk =>
  (w, c, None) \in dWCoZ_rej sk <=>
  (w, c) \in dWC_rej sk.
proof.
move => ?; split; first by smt(supp_dmap).
rewrite supp_dmap; smt(dWCoZ_rej_None).
qed.

local lemma p_rej_E sk :
  p_rej sk = 1%r - p_acc sk.
proof.
rewrite /p_rej.
have ->: (fun wcz : (W * C * Z option)=> let (w, c, oz) = wcz in oz = None) =
      predC (fun wcz => let (w, c, oz) = wcz in oz <> None) by apply fun_ext => ? /#.
have <-: weight (dWCoZ sk) = 1%r by apply dWCoZ_ll.
rewrite mu_not; rewrite /p_acc => //.
qed.

local lemma dXYZ_acc_supp_m sk m x y z :
  (x, y, z) \in dXYZ_acc (sk, m) => x.`2 = m.
proof. by rewrite /dXYZ_acc /= supp_dmap /#. qed.

local lemma dXYZ_acc1E sk m w c z :
  mu1 (dXYZ_acc (sk, m)) ((w, m), c, z) = mu1 (dWCZ_acc sk) (w, c, z).
proof.
by rewrite /dXYZ_acc /= dmap1E /(\o) /=; congr => /#.
qed.

local lemma dXYZ_acc_supp sk m w c z :
  (w, c, z) \in dWCZ_acc sk <=>
  ((w, m), c, z) \in dXYZ_acc (sk, m).
proof.
rewrite /dXYZ_acc /=.
split => H; first by rewrite supp_dmap => /#.
by rewrite supp_dmap in H => /#.
qed.

local lemma dXY_rej_supp_m sk m x y :
  (x, y) \in dXY_rej (sk, m) => x.`2 = m.
proof. by rewrite /dXY_rej /= supp_dmap /#. qed.

local lemma dXY_rej1E sk m w c :
  mu1 (dXY_rej (sk, m)) ((w, m), c) = mu1 (dWC_rej sk) (w, c).
proof.
by rewrite /dXY_rej /= dmap1E /(\o) /=; congr => /#.
qed.

local lemma dXY_rej_supp sk m w c :
  (w, c) \in dWC_rej sk <=>
  ((w, m), c) \in dXY_rej (sk, m).
proof.
rewrite /dXY_rej /=.
split => H; first by rewrite supp_dmap => /#.
by rewrite supp_dmap in H => /#.
qed.

(* Now state the alternative way of sampling transcript is correct *)
local lemma conditional_sampling_transcript sk :
  valid_sk sk =>
  dWCoZ sk = dlet (dbiased (p_acc sk)) (fun f =>
    if f then
      dWCoZ_acc sk
    else
      dWCoZ_rej sk).
proof.
move => H; rewrite -dmap_dbiased; first by apply dWCoZ_ll.
rewrite {1} (marginal_sampling (dWCoZ sk) (fun wcoz => let (w, c, oz) = wcoz in oz <> None)) /=.
congr; apply fun_ext => b /=; case b => _.
- rewrite /dWCoZ_acc H /=.
  congr; apply fun_ext => ? /#.
- rewrite /dWCoZ_rej H /=.
  congr; apply fun_ext => ? /#.
qed.

(* -- Lining up interface to ReprogRej -- *)

(* Helper module to call bypr... *)
local module LoopBody1 = {
  include var OLeft
  proc main() = {
    var w, c, oz;
    (w, c, oz) <$ dWCoZ sk;
    return (w, c, oz);
  }
}.

(* Helper module to call bypr... *)
local lemma pr_body1 x &m sk :
  OLeft.sk{m} = sk =>
  Pr[LoopBody1.main() @ &m : res = x] = mu1 (dWCoZ sk) x.
proof.
move => *.
byphoare (_ : (OLeft.sk = sk) ==> (res = x)) => //=.
proc.
rnd (fun r => r = x).
auto => /#.
qed.

local lemma dWCoZ_linearize sk :
  (dlet (commit sk)
        (fun (w_st : W * ST) =>
           dmap dC (fun (c0 : C) => (w_st.`1, c0, respond sk w_st.`2 c0)))) =
  (dmap (dWCoZ sk)
        (fun (w_c_oz : W * C * Z option) => (w_c_oz.`1, w_c_oz.`2, w_c_oz.`3))).
proof.
  rewrite /dWCoZ => /=.
  have ->: (fun (w_c_oz : W * C * Z option) => (w_c_oz.`1, w_c_oz.`2, w_c_oz.`3)) =
           (fun x => x) by smt().
  rewrite dmap_id; congr => /=.
  apply fun_ext => wst; case wst => ?? /=.
  rewrite /dmap /(\o) => /#.
qed.

local module LoopBody2 = {
  include var OLeft
  proc main() = {
    var w, c, oz, st;
    (w, st) <$ commit sk;
    c <$ dC;
    oz <- respond sk st c;
    return (w, c, oz);
  }
}.

local equiv hop_body2 :
  LoopBody1.main ~ LoopBody2.main :
  ={OLeft.sk} ==> ={res}.
proof.
proc.
rnd: *0 *0.
auto => /> &2.
split.
- move => * /=; congr; apply dWCoZ_linearize.
- move => _ wcoz H'. rewrite dWCoZ_linearize => //.
qed.

local lemma pr_body2 x &m sk :
  OLeft.sk{m} = sk =>
  Pr[LoopBody2.main() @ &m : res = x] = mu1 (dWCoZ sk) x.
proof.
move => *.
have <- : Pr[LoopBody1.main() @ &m : res = x] = Pr[LoopBody2.main() @ &m : res = x].
  byequiv.
  conseq (_ : ={OLeft.sk} ==> ={res}). trivial. trivial.
  apply hop_body2. trivial. trivial.
rewrite (pr_body1 x &m sk) => /#.
qed.

local module LoopBody3 = {
  include var OLeft
  proc main() = {
    var w, c, oz, f;
    f <$ dbiased (p_acc sk);
    if(f)
      (w, c, oz) <$ dWCoZ_acc sk;
    else
      (w, c, oz) <$ dWCoZ_rej sk;
    return (w, c, oz);
  }
}.

local equiv hop_body3 :
  LoopBody2.main ~ LoopBody3.main :
  ={OLeft.sk} /\ valid_sk OLeft.sk{1} ==> ={res}.
proof.
bypr res{1} res{2}; 1: auto => /#.
move => &1 &2 x eq_sk.
rewrite (pr_body2 x &1 OLeft.sk{1}) => //.
byphoare (_: (OLeft.sk = OLeft.sk{1}) ==> (res = x)) => //=; 2: by smt().
proc.
seq 1: f (p_acc OLeft.sk{1}) 
         (mu1 (dWCoZ_acc OLeft.sk{1}) x) 
         (p_rej OLeft.sk{1}) 
         (mu1 (dWCoZ_rej OLeft.sk{1}) x) #pre => //=.
- by auto.
- rnd; auto => /> /=.
  rewrite dbiasedE => /=.
  rewrite !clamp_id => //.
- rcondt 1 => //=.
  by rnd (pred1 x); skip => /> /#.
- rnd; auto => /> /=.
  rewrite dbiasedE => /=.
  rewrite !clamp_id => //.
  by rewrite p_rej_E => //=.
- rcondf 1 => //=.
  by rnd (pred1 x); skip => /#.
- move => *; subst.
  rewrite conditional_sampling_transcript; 1: smt().
  rewrite dlet1E /=.
  rewrite sum_over_bool => /=.
  rewrite !dbiased1E => /=.
  rewrite !clamp_id => //=.
  smt(p_rej_E).
qed.

local module LoopBody4 = {
  include var OLeft
  proc main() = {
    var w, c, z, f;
    f <$ dbiased (p_acc sk);
    z <- witness; (* silences unused variable warning *)
    if(f)
      (w, c, z) <$ dWCZ_acc sk;
    else
      (w, c) <$ dWC_rej sk;
    return (w, c, f, z);
  }
}.

local equiv hop_body4 :
  LoopBody3.main ~ LoopBody4.main :
  ={OLeft.sk} /\ valid_sk OLeft.sk{1} ==> (res{1}.`1 = res{2}.`1 /\
                         res{1}.`2 = res{2}.`2 /\
                         (res{1}.`3 <> None <=> res{2}.`3) /\
                         (res{1}.`3 <> None => oget res{1}.`3 = res{2}.`4)).
proof.
proc; auto.
seq 1 1: (#pre /\ ={f}); 1: auto.
seq 0 1: (#pre); 1: auto.
if; 1: by auto.
- rnd (fun wcoz => let (w, c, oz) = wcoz in (w, c, oget oz))
      (fun wcz => let (w, c, z) = wcz in (w, c, Some z)).
  auto => /> *.
  split => *; first smt().
  split => />.
  - move => wcz; case wcz => w c z ?.
    rewrite dmap1E.
    apply mu_eq_support => x; case x => w' c' z' H /=.
    rewrite /(\o) /pred1 => /=.
    smt(dWCoZ_acc_supp).
  - move => _ wcoz; case wcoz => ????.
    move => />; smt(dmap_supp dWCoZ_acc_supp).
- rnd (fun wcoz => let (w, c, oz) = wcoz in (w, c))
      (fun wc => let (w, c) = wc in (w, c, None)).
  auto => /> *. (* terrifying *)
  split => [|_]; first by smt().
  split => [[??] _ |_ [???]] /=; 2: smt(dWCoZ_rej_supp dWCoZ_rej_None).
  rewrite dmap1E /(\o) /pred1.
  by apply mu_eq_support; smt(dWCoZ_rej_None).
qed.

local equiv hop_body2_4 :
  LoopBody2.main ~ LoopBody4.main :
  ={OLeft.sk} /\ valid_sk OLeft.sk{1} ==> (res{1}.`1 = res{2}.`1 /\
                         res{1}.`2 = res{2}.`2 /\
                         (res{1}.`3 <> None <=> res{2}.`3) /\
                         (res{1}.`3 <> None => oget res{1}.`3 = res{2}.`4)).
proof.
transitivity LoopBody3.main
  (={OLeft.sk} /\ valid_sk OLeft.sk{1} ==> ={res})
  (={OLeft.sk} /\ valid_sk OLeft.sk{1} ==> res{1}.`1 = res{2}.`1 /\
  res{1}.`2 = res{2}.`2 /\
  (res{1}.`3 <> None <=> res{2}.`3) /\
  (res{1}.`3 <> None => oget res{1}.`3 = res{2}.`4)); 1, 2: smt().
- by apply hop_body3.
apply hop_body4.
qed.

(* -- Left game transforms -- *)

(* Loop-bodies brute-forcing spaghetti...
 * RIP signal-to-noise ratio *)

local module OLeft_body2 : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, oz, i;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    i <- 0;
    oz <- None;
    while(oz = None /\ i < kappa) {
      (w, c, oz) <@ LoopBody2.main();
      RO.set((w, m), c);
      i <- i + 1;
    }
    return if oz <> None then Some (w, c, oget oz) else None;
  }
}.

local equiv OLeft_body2_call :
  OLeft.sign ~ OLeft_body2.sign :
  ={OLeft.sk, m, RO.m} ==> ={res, RO.m}.
proof.
by proc; inline LoopBody2.main; sim.
qed.

local module OLeft_body4 : Oracle_i = {
  include var OLeft[init, h]
 
  proc sign(m: M) = {
    var w, c, f, z, i;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      (w, c, f, z) <@ LoopBody4.main();
      RO.set((w, m), c);
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local equiv OLeft_loop_bodies_hop :
  OLeft_body2.sign ~ OLeft_body4.sign :
  ={OLeft.sk, m, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
proc. seq 4 5: (#pre /\ i{1} = 0 /\ i{2} = 0 /\ oz{1} = None /\ !f{2}); first by auto.
while (={m, OLeft.sk, i, RO.m} /\ (oz{1} = None <=> !f{2})
         /\ (f{2} => (={w, c} /\ oget oz{1} = z{2})) /\ valid_sk OLeft.sk{1}); 2: by auto => /#.
wp.
call(_: ={RO.m, arg} ==> ={RO.m}).
- by proc; auto => />.
call hop_body2_4; auto => /#.
qed.

(* Replaces the transcript generation with the above *)
local module OLeft_cond : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, z, f, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f)
        (w, c, z) <$ dWCZ_acc sk;
      else
        (w, c) <$ dWC_rej sk;
      RO.set((w, m), c);
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local equiv OLeft_cond_hop : OLeft_body4.sign ~ OLeft_cond.sign :
  ={m, OLeft.sk, RO.m} ==> ={res, RO.m}.
proof.
by proc; inline LoopBody4.main; sim.
qed.

local module OLeft_move_reprog : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, z, f, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f) {
        (w, c, z) <$ dWCZ_acc sk;
        RO.set((w, m), c);
      }
      else {
        (w, c) <$ dWC_rej sk;
        RO.set((w, m), c);
      }
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local equiv OLeft_move_reprog_hop :
  OLeft_cond.sign ~ OLeft_move_reprog.sign :
  ={m, OLeft.sk, RO.m} ==> ={res, RO.m}.
proof.
proc.
seq 5 5: (#pre /\ ={w, c, z} /\ i{1} = 0 /\ i{2} = 0 /\ !f{1} /\ !f{2}); first by auto.
while (={m, OLeft.sk, w, c, z, i, f, RO.m}); last by auto => />.
seq 2 2: (={m, OLeft.sk, w, c, z, i, f, RO.m}); first by sim.
if; 1: smt(); 1,2: by sim.
qed.

local module OLeft_drop_rej_info : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, wr, c, cr, z, f, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f) {
        (w, c, z) <$ dWCZ_acc sk;
        RO.set((w, m), c);
      }
      else {
        (wr, cr) <$ dWC_rej sk;
        RO.set((wr, m), cr);
      }
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local equiv OLeft_drop_rej_info_hop :
  OLeft_move_reprog.sign ~ OLeft_drop_rej_info.sign :
  ={m, OLeft.sk, RO.m} ==> ={res, RO.m}.
proof.
proc.
seq 5 5: (#pre /\ ={w, c, z} /\ i{1} = 0 /\ i{2} = 0 /\ !f{1} /\ !f{2}); first by auto.
while (={m, OLeft.sk, RO.m, z, i, f} /\ (f{1} => ={w, c})); last by auto => /#.
seq 2 2: (={m, OLeft.sk, RO.m, z, i, f}); first by sim.
if; 1: smt().
- inline RO.set; auto => /#.
- inline RO.set; auto => /#.
qed.

local equiv OLeft_restructure_all :
  OLeft.sign ~ OLeft_drop_rej_info.sign :
  ={m, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
transitivity OLeft_body2.sign
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m})
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}); 1, 2: smt().
- by conseq OLeft_body2_call => //.
transitivity OLeft_body4.sign
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m})
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}); 1, 2: smt().
- by apply OLeft_loop_bodies_hop.
transitivity OLeft_cond.sign
  (={arg, OLeft.sk, RO.m} ==> ={res, RO.m})
  (={OLeft.sk, arg, RO.m} ==> ={res, RO.m}); 1, 2: smt().
- by apply OLeft_cond_hop.
transitivity OLeft_move_reprog.sign
  (={arg, OLeft.sk, RO.m} ==> ={res, RO.m})
  (={arg, OLeft.sk, RO.m} ==> ={res, RO.m}); 1, 2: smt().
- by apply OLeft_move_reprog_hop.
exact OLeft_drop_rej_info_hop.
qed.

(** -- Right game transforms -- **)

local module ORight_body2 : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, oz, i;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;

    i <- 0;
    oz <- None;
    while(oz = None /\ i < kappa) {
      (w, c, oz) <@ LoopBody2.main();
      i <- i + 1;
    }
    if(oz <> None)
      RO.set((w, m), c);
    return if oz <> None then Some (w, c, oget oz) else None;
  }
}.

local equiv ORight_body2_call :
  ORight.sign ~ ORight_body2.sign :
  ={OLeft.sk, m, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
by proc; inline LoopBody2.main; sim.
qed.

local module ORight_body4 : Oracle_i = {
  include var OLeft[init, h]
 
  proc sign(m: M) = {
    var w, c, f, z, i;
    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      (w, c, f, z) <@ LoopBody4.main();
      i <- i + 1;
    }
    if(f)
      RO.set((w, m), c);
    return if f then Some (w, c, z) else None;
  }
}.

local equiv ORight_loop_bodies_hop :
  ORight_body2.sign ~ ORight_body4.sign :
  ={OLeft.sk, m, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
proc.
seq 4 5: (#pre /\ ={w, c} /\ i{1} = 0 /\ i{2} = 0 /\ oz{1} = None /\ !f{2} /\ valid_sk OLeft.sk{1}); first by auto.
seq 1 1: (={RO.m, OLeft.sk, m, w, c, i} /\ (oz{1} <> None <=> f{2}) /\
           (oz{1} <> None => oget oz{1} = z{2}) /\ valid_sk OLeft.sk{1}); last first.
- if => /> //.
  call(_: ={arg, RO.m} ==> ={RO.m}); last by auto => /#.
  by proc; auto => /#.
while(#post); last by skip => /#.
wp; call hop_body2_4; auto => /#.
qed.

(* Replaces the transcript generation with the above *)
local module ORight_cond : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, z, f, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f)
        (w, c, z) <$ dWCZ_acc sk;
      else
        (w, c) <$ dWC_rej sk;
      i <- i + 1;
    }
    if(f)
      RO.set((w, m), c);
    return if f then Some (w, c, z) else None;
  }
}.

local equiv ORight_cond_hop : ORight_body4.sign ~ ORight_cond.sign :
  ={m, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
by proc; inline LoopBody4.main; sim.
qed.

local module ORight_move_reprog : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, z, f, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f) {
        (w, c, z) <$ dWCZ_acc sk;
        RO.set((w, m), c);
      }
      else {
        (w, c) <$ dWC_rej sk;
      }
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local equiv ORight_move_reprog_hop :
  ORight_cond.sign ~ ORight_move_reprog.sign :
  ={m, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
proc; seq 5 5: (#pre /\ ={w, c, z, i, f} /\ !f{1}); first by auto.
seq 1 1: (={m, OLeft.sk, w, c, z, i, f} /\
           (f{1} => RO.m{1}.[(w{1}, m{1}) <- c{1}] = RO.m{2}) /\
           (!f{1} => ={RO.m})).
- while (#post); last by auto => />.
  seq 2 2: (={m, OLeft.sk, w, c, z, i, f, z, RO.m}); first by sim => /#.
  if; first by smt().
  (* Not sure why these `auto` takes a few seconds... *)
  + by inline RO.set; auto.
  + by auto.
- if{1}; last by auto => />.
  by inline RO.set; auto => />.
qed.

local module ORight_drop_rej_info : Oracle_i = {
  include var OLeft[init, h]

  proc sign(m: M) = {
    var w, c, z, f, i;

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f) {
        (w, c, z) <$ dWCZ_acc sk;
        RO.set((w, m), c);
      }
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local equiv ORight_drop_rej_info_hop :
  ORight_move_reprog.sign ~ ORight_drop_rej_info.sign :
  ={m, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
proc.
seq 5 5: (#pre /\ ={w, c, z} /\ i{1} = 0 /\ i{2} = 0 /\ !f{1} /\ !f{2}); first by auto.
while (={m, OLeft.sk, RO.m, z, i, f} /\ valid_sk OLeft.sk{1} /\ (f{1} => ={w, c}));
  last by auto => /#.
seq 2 2: (={m, OLeft.sk, RO.m, z, i, f} /\ valid_sk OLeft.sk{1}); first by auto => />.
if; 1: smt().
- by conseq/>; sim.
- auto => />; smt(dWC_rej_ll).
qed.

local equiv ORight_restructure_all :
  ORight.sign ~ ORight_drop_rej_info.sign :
  ={m, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}.
proof.
transitivity ORight_body2.sign
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m})
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}); 1, 2: smt().
- by apply ORight_body2_call.
transitivity ORight_body4.sign
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m})
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}); 1, 2: smt().
- by apply ORight_loop_bodies_hop.
transitivity ORight_cond.sign
  (={arg, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m})
  (={OLeft.sk, arg, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}); 1, 2: smt().
- by apply ORight_cond_hop.
transitivity ORight_move_reprog.sign
  (={arg, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m})
  (={arg, OLeft.sk, RO.m} /\ valid_sk OLeft.sk{1} ==> ={res, RO.m}); 1, 2: smt().
- by apply ORight_move_reprog_hop.
exact ORight_drop_rej_info_hop.
qed.

(** -- Reduction to ReprogRej -- **)

local module OReduction(O: Guess.Oracle) : Oracle = {
  var sk : SK

  proc init(sk_i : SK) = {
    sk <- sk_i;
  }

  proc h = O.h

  proc sign(m: M) = {
    var w, c, z, f, i, wm, m';

    (* Silences unused variables warning *)
    w <- witness;
    c <- witness;
    z <- witness;

    i <- 0;
    f <- false;
    while(!f /\ i < kappa) {
      f <$ dbiased (p_acc sk);
      z <- witness;
      if(f) {
        (wm, c, z) <@ O.reprog_acc((sk, m));
        (w, m') <- wm;
      }
      else {
        O.reprog_rej((sk, m));
      }
      i <- i + 1;
    }
    return if f then Some (w, c, z) else None;
  }
}.

local lemma ReductionO_ll : forall (O <: Guess.Oracle),
  islossless O.h =>
  islossless O.reprog_acc =>
  islossless O.reprog_rej =>
  islossless OReduction(O).h /\ islossless OReduction(O).sign.
proof.
move => O h_ll acc_ll rej_ll.
split; first by apply h_ll.
proc; sp.
while(true) (kappa - i) => [t|]; last by skip => />; smt(kappa_gt0).
wp; seq 2: (i < kappa /\ kappa - i = t) => //.
- by auto => />; smt(dbiased_ll).
- if; wp; call(:true); skip => /#.
- by conseq/>; auto.
qed.

local module (Reduction : Guess.Adversary) (ORej: Guess.Oracle) = {
  include var OReduction(ORej) [-sign, h]
  (* Hack that stops ORej{-Reduction} from writing count *)
  include var Count(OReduction(ORej)) [-init]

  proc distinguish() = {
    var b;
    var pk, sk;
    (pk, sk) <$ keygen;
    OReduction(ORej).init(sk);
    b <@ Adv(OReduction(ORej)).distinguish(pk);
    return b;
  }
}.

local lemma Reduction_ll : forall (O <: Guess.Oracle {-Adv}),
  islossless O.h =>
  islossless O.reprog_acc =>
  islossless O.reprog_rej =>
  islossless Reduction(O).distinguish.
proof.
move => O h_ll acc_ll rej_ll.
proc.
inline OReduction(O).init.
seq 1: true => //; first by auto; smt(keygen_ll).
islossless.
apply (A_ll (OReduction(O))) => //.
suff: islossless OReduction(O).h /\ islossless OReduction(O).sign by trivial.
exact (ReductionO_ll O).
qed.

local module (ReductionCount : Guess.Adversary) (ORej: Guess.Oracle) = {
  proc distinguish() = {
    var b;
    var pk, sk;
    (pk, sk) <$ keygen;
    Count(OReduction(ORej)).init(sk);
    b <@ Adv(Count(OReduction(ORej))).distinguish(pk);
    return b;
  }
}.

local lemma ReductionCount_bound : forall (ORej <: Guess.Oracle{-Adv, -Count, -OReduction}),
  hoare[ ReductionCount(ORej).distinguish :
    Count.countS = 0 /\ Count.countH = 0 ==> 
    Count.countS <= qs /\ Count.countH <= qh ].
proof.
move => ORej; proc.
inline Count(OReduction(ORej)).init.
seq 1: #pre; 1: by auto.
call (A_bound (OReduction(ORej))) => //.
by auto => //.
qed.

local lemma ReductionCount_hop : forall (ORej <: Guess.Oracle{-Adv, -Guess.Count, -Count, -OReduction}),
  equiv[Reduction(Guess.Count(ORej)).distinguish ~
        ReductionCount(ORej).distinguish :
    ={glob Adv, glob ORej} /\
    Guess.Count.countH{1} = 0 /\
    Guess.Count.countR{1} = 0 /\
    Count.countH{2} = 0 /\
    Count.countS{2} = 0 ==>
    Guess.Count.countH{1} = Count.countH{2} /\
    Guess.Count.countR{1} <= kappa * Count.countS{2}].
proof.
move => ORej; proc.
inline OReduction(Guess.Count(ORej)).init.
seq 3 2: (#pre /\ ={pk, OReduction.sk}).
- inline Count(OReduction(ORej)).init.
  by conseq/>; sim.
call (_: ={glob ORej, OReduction.sk} /\
         Guess.Count.countH{1} = Count.countH{2} /\
         Guess.Count.countR{1} <= kappa * Count.countS{2}); last by auto.
- proc.
  seq 1 1: #pre; 1: by auto.
  call(_: true); by skip => />.

- proc; inline OReduction(ORej).sign.
  (* don't sp or you'll see something terrifying *)
  wp; seq 5 7: (={glob ORej, w, c, z, i, f, m, OReduction.sk} /\
    i{1} = 0 /\
    Guess.Count.countH{1} = Count.countH{2} /\
    Guess.Count.countR{1} + kappa <= kappa * Count.countS{2}).
  + by auto => /> /#.

  while(={glob ORej, w, c, z, i, f, m, OReduction.sk} /\
    0 <= i{1} <= kappa /\
    Guess.Count.countH{1} = Count.countH{2} /\
    Guess.Count.countR{1} + kappa - i{1} <= kappa * Count.countS{2});
    last by skip => />; smt(kappa_gt0).
  seq 2 2: (={glob ORej, w, c, z, i, f, m, OReduction.sk} /\
            (0 <= i{1} /\ i{1} < kappa) /\
             Guess.Count.countH{1} = Count.countH{2} /\
             Guess.Count.countR{1} + kappa - i{1} <= kappa * Count.countS{2}).
  + by auto.
  if; first by smt().
  + inline Guess.Count(ORej).reprog_acc.
    sp; wp; call(_: true).
    skip => /> *.
    smt(kappa_gt0).
  + inline Guess.Count(ORej).reprog_rej.
    sp; wp; call(_: ={arg, glob ORej} ==> ={res, glob ORej}); first by sim.
    by skip => />; smt(kappa_gt0).
qed.

local lemma Reduction_bound : forall (RejO <: Guess.Oracle{-Adv, -Guess.Count, -Count, -OReduction}), 
  hoare [ Reduction(Guess.Count(RejO)).distinguish :
    Guess.Count.countR = 0 /\ Guess.Count.countH = 0 ==> 
    Guess.Count.countR <= kappa * qs /\ Guess.Count.countH <= qh ].
proof.
move => RejO.
conseq (ReductionCount_hop RejO) (ReductionCount_bound RejO); smt(kappa_gt0).
qed.

local equiv reduction_correct_left :
  Game(Adv, OLeft).main ~ Guess.Game(Guess.OLeft, Reduction).main :
  ={glob Adv} ==> ={res}.
proof.
proc.
inline RO.init OLeft.init Guess.OLeft.init Reduction(Guess.OLeft).distinguish.
wp; inline OReduction(Guess.OLeft).init.
seq 4 4: (#pre /\ ={RO.m, pk} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}).
- by wp; rnd; auto => /> /#.
call (_ : ={RO.m} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}); last by skip => />.
- by proc; inline RO.get; auto.
- transitivity OLeft_drop_rej_info.sign
  ((={arg} /\ ={RO.m, OLeft.sk} /\ valid_sk OLeft.sk{1}) ==>
   (={res} /\ ={RO.m, OLeft.sk} /\ valid_sk OLeft.sk{1}))
  ((={arg} /\ ={RO.m} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}) ==>
   (={res} /\ ={RO.m} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}));
    1, 2: smt().
  + conseq OLeft_restructure_all; 1, 2: smt().
  proc; conseq/>.
  seq 5 5: (#pre /\ ={w, c, z, i, f}); 1: by auto.
  while(#pre); last by skip => />.
  seq 2 2: (={m, RO.m, w, c, z, i, f} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1});
    first by auto.
  wp.
  if; first by auto.
  + inline Guess.OLeft.reprog_acc RO.set.
    sp; wp; conseq />.
    rnd (fun wcz => let (w, c, z) = wcz in ((w, m{2}), c, z))
        (fun xyz => let (wm, c, z) = xyz in let (w, m) = wm in (w, c, z)).
    (* rnd makes me very sad *)
    skip => /> *.
    (* Don't ask about these intro patterns *)
    split => /= [[[??]??]|_]; first smt(dXYZ_acc_supp_m).
    split => /= [[??]??| _ [???]] /=; first smt(dXYZ_acc1E dXYZ_acc_supp_m).
    smt(dXYZ_acc_supp_m dXYZ_acc_supp).
  + inline Guess.OLeft.reprog_rej.
    sp; wp; conseq />.
    seq 1 1: (={RO.m} /\ (wr{1}, m{1}) = x{2} /\ cr{1} = y{2}); last by inline RO.set; auto.
    conseq />.
    rnd (fun wc => let (w, c) = wc in ((w, m{2}), c))
        (fun xy => let (x, y) = xy in let (w, m) = x in (w, y)).
    skip => /> *.
    (* you hate to see it *)
    split => [[[??]?]?|_] /=; first smt(dXY_rej_supp_m).
    split => [[[??]?]|_ [??]?] /=; first smt(dXY_rej1E dXY_rej_supp_m).
    smt(dXY_rej_supp_m dXY_rej_supp).
qed.

local equiv reduction_correct_right :
  Game(Adv, ORight).main ~ Guess.Game(Guess.ORight, Reduction).main :
  ={glob Adv} ==> ={res}.
proof.
proc.
inline RO.init OLeft.init Guess.OLeft.init Reduction(Guess.ORight).distinguish.
wp; inline OReduction(Guess.ORight).init.
seq 4 4: (#pre /\ ={RO.m, pk} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}).
- by wp; rnd; auto => /> /#.
call (_ : ={RO.m} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}); last by skip => />.
- by proc; inline RO.get; auto.
- transitivity ORight_drop_rej_info.sign
  ((={arg} /\ ={RO.m, OLeft.sk} /\ valid_sk OLeft.sk{1}) ==>
   (={res} /\ ={RO.m, OLeft.sk} /\ valid_sk OLeft.sk{1}))
  ((={arg} /\ ={RO.m} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}) ==>
   (={res} /\ ={RO.m} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1}));
    1, 2: smt().
  + conseq ORight_restructure_all; 1, 2: smt().
  proc; conseq/>.
  seq 5 5: (#pre /\ ={w, c, z, i, f}); 1: by auto.
  while(#pre); last by skip => />.
  seq 2 2: (={m, RO.m, w, c, z, i, f} /\ OLeft.sk{1} = OReduction.sk{2} /\ valid_sk OLeft.sk{1});
    first by auto.
  wp.
  if; 3: by (inline Guess.ORight.reprog_rej; auto).
  + by auto.
  inline Guess.OLeft.reprog_acc RO.set.
  sp; wp; conseq />.
  rnd (fun wcz => let (w, c, z) = wcz in ((w, m{2}), c, z))
      (fun xyz => let (wm, c, z) = xyz in let (w, m) = wm in (w, c, z)).
  (* copied from proof above for left game *)
  skip => /> *.
  split => /= [[[??]??]|_]; first smt(dXYZ_acc_supp_m).
  split => /= [[??]??| _ [???]] /=; first smt(dXYZ_acc1E dXYZ_acc_supp_m).
  smt(dXYZ_acc_supp_m dXYZ_acc_supp).
qed.

lemma adv_bound &m :
  `| Pr[Game(Adv, OLeft).main() @ &m : res] -
     Pr[Game(Adv, ORight).main() @ &m : res] | <= qh%r * (kappa * qs)%r * alpha.
proof.
have ->: Pr[Game(Adv, OLeft).main() @ &m : res] =
         Pr[Guess.Game(Guess.OLeft, Reduction).main() @ &m: res].
- byequiv => //.
  conseq reduction_correct_left => //.
have ->: Pr[Game(Adv, ORight).main() @ &m : res] =
         Pr[Guess.Game(Guess.ORight, Reduction).main() @ &m: res].
- byequiv => //.
  conseq reduction_correct_right => //.
apply (Guess.adv_bound Reduction).
move => ORej.
apply (Reduction_ll ORej).
move => ORej.
conseq (Reduction_bound ORej).
qed.

end section Analysis.
